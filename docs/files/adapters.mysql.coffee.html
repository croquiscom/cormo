<!DOCTYPE html><html lang="en"><head><meta charset="utf-8"><meta http-equiv="X-UA-Compatible" content="IE=edge"><meta name="viewport" content="width=device-width, initial-scale=1.0, maximum-scale=1.0, user-scalable=no"><title>CORMO - adapters/mysql.coffee</title><script>if (location.protocol.match(/^http/) && location.pathname.match('\.html') === null && location.pathname.slice(-1) !== '/') {
  location.href = location.href + '/';
}</script><link href="../bootstrap-3.2.0-dist/css/bootstrap.min.css" rel="stylesheet" type="text/css"><!--[if lt IE 9]><script src="https://oss.maxcdn.com/html5shiv/3.7.2/html5shiv.min.js"></script><script src="https://oss.maxcdn.com/respond/1.4.2/respond.min.js"></script><![endif]--><link href="../google-code-prettify/prettify.css" rel="stylesheet" type="text/css"><link href="../style.css" rel="stylesheet" type="text/css"></head><body data-spy="scroll" data-target=".sidebar"><nav class="navbar navbar-default navbar-fixed-top" role="navigation"><div class="navbar-header"><button class="navbar-toggle collapsed" type="button" data-toggle="collapse" data-target="#top-navigation-collapse"><span class="sr-only">Toggle navigation</span><span class="icon-bar"></span><span class="icon-bar"></span><span class="icon-bar"></span></button><div class="collapse navbar-collapse" id="top-navigation-collapse"><ul class="nav navbar-nav"><li><a href="../index.html">Home</a></li><li class="dropdown"><a class="dropdown-toggle" data-toggle="dropdown" href="#">Guides <span class="caret"></span></a><ul class="dropdown-menu"><li><a href="../guides/Aggregation.html">Aggregation</a></li><li><a href="../guides/Association.html">Association</a></li><li><a href="../guides/Callback.html">Callback</a></li><li><a href="../guides/Constraint.html">Constraint</a></li><li><a href="../guides/CreateRecords.html">Create records</a></li><li><a href="../guides/DefineModels.html">Define models</a></li><li><a href="../guides/Geospatial.html">Geospatial</a></li><li><a href="../guides/Miscellaneous.html">Miscellaneous</a></li><li><a href="../guides/Query.html">Query</a></li><li><a href="../guides/Validation.html">Validation</a></li></ul></li><li><a href="../modules/index.html">Modules</a></li><li><a href="../classes/index.html">Classes</a></li><li class="dropdown active"><a class="dropdown-toggle" data-toggle="dropdown" href="#">Files - adapters/mysql.coffee <span class="caret"></span></a><ul class="dropdown-menu"><li><a href="../files/index.coffee.html">index.coffee</a></li><li><a href="../files/query.coffee.html">query.coffee</a></li><li><a href="../files/types.coffee.html">types.coffee</a></li><li><a href="../files/adapters.base.coffee.html">adapters/base.coffee</a></li><li><a href="../files/adapters.mongodb.coffee.html">adapters/mongodb.coffee</a></li><li><a href="../files/adapters.mysql.coffee.html">adapters/mysql.coffee</a></li><li><a href="../files/adapters.postgresql.coffee.html">adapters/postgresql.coffee</a></li><li><a href="../files/adapters.redis.coffee.html">adapters/redis.coffee</a></li><li><a href="../files/adapters.sql_base.coffee.html">adapters/sql_base.coffee</a></li><li><a href="../files/adapters.sqlite3.coffee.html">adapters/sqlite3.coffee</a></li><li><a href="../files/adapters.sqlite3_memory.coffee.html">adapters/sqlite3_memory.coffee</a></li><li><a href="../files/command.index.coffee.html">command/index.coffee</a></li><li><a href="../files/command.remote-console.coffee.html">command/remote-console.coffee</a></li><li><a href="../files/connection.association.coffee.html">connection/association.coffee</a></li><li><a href="../files/connection.index.coffee.html">connection/index.coffee</a></li><li><a href="../files/connection.manipulate.coffee.html">connection/manipulate.coffee</a></li><li><a href="../files/model.cache.coffee.html">model/cache.coffee</a></li><li><a href="../files/model.callback.coffee.html">model/callback.coffee</a></li><li><a href="../files/model.index.coffee.html">model/index.coffee</a></li><li><a href="../files/model.persistence.coffee.html">model/persistence.coffee</a></li><li><a href="../files/model.query.coffee.html">model/query.coffee</a></li><li><a href="../files/model.timestamp.coffee.html">model/timestamp.coffee</a></li><li><a href="../files/model.validate.coffee.html">model/validate.coffee</a></li><li><a href="../files/util.index.coffee.html">util/index.coffee</a></li><li><a href="../files/util.inflector.coffee.html">util/inflector.coffee</a></li></ul></li></ul><div class="options"><label class="checkbox"><input id="options-private" type="checkbox"> Private </label></div></div></div></nav><div class="container-fluid content"><div class="row"><div class="hidden-xs sidebar col-sm-3" data-spy="affix"><div class="cormo-sidenav"><div class="panel panel-default"><div class="panel-collapse collapse in" id="undefined_body"><ul class="nav nav-pills nav-stacked"><li><a href="../files/index.coffee.html">index.coffee</a></li><li><a href="../files/query.coffee.html">query.coffee</a></li><li><a href="../files/types.coffee.html">types.coffee</a></li></ul></div></div><div class="panel panel-default"><div class="panel-heading" data-toggle="collapse" data-target="#adapters__body">adapters/<span class="pull-right glyphicon"></span></div><div class="panel-collapse collapse in" id="adapters__body"><ul class="nav nav-pills nav-stacked"><li><a href="../files/adapters.base.coffee.html">base.coffee</a></li><li><a href="../files/adapters.mongodb.coffee.html">mongodb.coffee</a></li><li class="active"><a href="../files/adapters.mysql.coffee.html">mysql.coffee</a></li><li><a href="../files/adapters.postgresql.coffee.html">postgresql.coffee</a></li><li><a href="../files/adapters.redis.coffee.html">redis.coffee</a></li><li><a href="../files/adapters.sql_base.coffee.html">sql_base.coffee</a></li><li><a href="../files/adapters.sqlite3.coffee.html">sqlite3.coffee</a></li><li><a href="../files/adapters.sqlite3_memory.coffee.html">sqlite3_memory.coffee</a></li></ul></div></div><div class="panel panel-default"><div class="panel-heading" data-toggle="collapse" data-target="#command__body">command/<span class="pull-right glyphicon"></span></div><div class="panel-collapse collapse in" id="command__body"><ul class="nav nav-pills nav-stacked"><li><a href="../files/command.index.coffee.html">index.coffee</a></li><li><a href="../files/command.remote-console.coffee.html">remote-console.coffee</a></li></ul></div></div><div class="panel panel-default"><div class="panel-heading" data-toggle="collapse" data-target="#connection__body">connection/<span class="pull-right glyphicon"></span></div><div class="panel-collapse collapse in" id="connection__body"><ul class="nav nav-pills nav-stacked"><li><a href="../files/connection.association.coffee.html">association.coffee</a></li><li><a href="../files/connection.index.coffee.html">index.coffee</a></li><li><a href="../files/connection.manipulate.coffee.html">manipulate.coffee</a></li></ul></div></div><div class="panel panel-default"><div class="panel-heading" data-toggle="collapse" data-target="#model__body">model/<span class="pull-right glyphicon"></span></div><div class="panel-collapse collapse in" id="model__body"><ul class="nav nav-pills nav-stacked"><li><a href="../files/model.cache.coffee.html">cache.coffee</a></li><li><a href="../files/model.callback.coffee.html">callback.coffee</a></li><li><a href="../files/model.index.coffee.html">index.coffee</a></li><li><a href="../files/model.persistence.coffee.html">persistence.coffee</a></li><li><a href="../files/model.query.coffee.html">query.coffee</a></li><li><a href="../files/model.timestamp.coffee.html">timestamp.coffee</a></li><li><a href="../files/model.validate.coffee.html">validate.coffee</a></li></ul></div></div><div class="panel panel-default"><div class="panel-heading" data-toggle="collapse" data-target="#util__body">util/<span class="pull-right glyphicon"></span></div><div class="panel-collapse collapse in" id="util__body"><ul class="nav nav-pills nav-stacked"><li><a href="../files/util.index.coffee.html">index.coffee</a></li><li><a href="../files/util.inflector.coffee.html">inflector.coffee</a></li></ul></div></div></div></div><div class="col-sm-9 col-sm-offset-3"><section><h1>adapters/mysql.coffee</h1></section><pre class="prettyprint">try
  mysql = require 'mysql'
catch e
  console.log 'Install mysql module to use this adapter'
  process.exit 1

SQLAdapterBase = require './sql_base'
types = require '../types'

_ = require 'lodash'
async = require 'async'
stream = require 'stream'

_typeToSQL = (property, support_fractional_seconds) -&gt;
  if property.array
    return 'TEXT'
  switch property.type_class
    when types.String then &quot;VARCHAR(#{property.type.length or 255})&quot;
    when types.Number then 'DOUBLE'
    when types.Boolean then 'BOOLEAN'
    when types.Integer then 'INT'
    when types.GeoPoint then 'POINT'
    when types.Date
      if support_fractional_seconds then 'DATETIME(3)' else 'DATETIME'
    when types.Object then 'TEXT'
    when types.Text then 'TEXT'

_propertyToSQL = (property, support_fractional_seconds) -&gt;
  type = _typeToSQL property, support_fractional_seconds
  if type
    if property.required
      type += ' NOT NULL'
    else
      type += ' NULL'
    return type

##
# Adapter for MySQL
# @namespace adapter
class MySQLAdapter extends SQLAdapterBase
  key_type: types.Integer
  support_geopoint: true
  support_string_type_with_length: true
  native_integrity: true
  _escape_ch: '`'

  ##
  # Creates a MySQL adapter
  constructor: (connection) -&gt;
    @_connection = connection

  _query: (sql, data, callback) -&gt;
    #console.log 'MySQLAdapter:', sql
    @_client.query sql, data, callback

  _getTables: (callback) -&gt;
    @_query &quot;SHOW TABLES&quot;, (error, tables) -&gt;
      return callback error if error
      tables = tables.map (table) -&gt;
        key = Object.keys(table)[0]
        table[key]
      callback null, tables

  _getSchema: (table, callback) -&gt;
    @_query &quot;SHOW COLUMNS FROM `#{table}`&quot;, (error, columns) -&gt;
      return callback error if error
      schema = {}
      for column in columns
        type = if /^varchar\((\d*)\)/i.test column.Type
          new types.String(RegExp.$1)
        else if /^double/i.test column.Type
          new types.Number()
        else if /^tinyint\(1\)/i.test column.Type
          new types.Boolean()
        else if /^int/i.test column.Type
          new types.Integer()
        else if /^point/i.test column.Type
          new types.GeoPoint()
        else if /^datetime/i.test column.Type
          new types.Date()
        else if /^text/i.test column.Type
          new types.Object()
        schema[column.Field] = type: type, required: column.Null is 'NO'
      callback null, schema

  _getIndexes: (callback) -&gt;
    @_query &quot;SELECT * FROM INFORMATION_SCHEMA.STATISTICS WHERE TABLE_SCHEMA = ? ORDER BY SEQ_IN_INDEX&quot;, [@_database], (error, rows) -&gt;
      return callback error if error
      indexes = {}
      for row in rows
        indexes_of_table = indexes[row.TABLE_NAME] or= {}
        (indexes_of_table[row.INDEX_NAME] or= {})[row.COLUMN_NAME] = 1
      callback null, indexes

  _getForeignKeys: (callback) -&gt;
    @_query &quot;SELECT * FROM INFORMATION_SCHEMA.KEY_COLUMN_USAGE WHERE REFERENCED_TABLE_NAME IS NOT NULL AND CONSTRAINT_SCHEMA = ?&quot;, [@_database], (error, rows) -&gt;
      return callback error if error
      foreign_keys = {}
      for row in rows
        foreign_keys_of_table = foreign_keys[row.TABLE_NAME] or= {}
        foreign_keys_of_table[row.COLUMN_NAME] = row.REFERENCED_TABLE_NAME
      callback null, foreign_keys

  ## @override AdapterBase::getSchemas
  getSchemas: (callback) -&gt;
    async.auto
      get_tables: (callback) =&gt;
        @_getTables callback
      get_table_schemas: ['get_tables', (results, callback) =&gt;
        table_schemas = {}
        async.each results.get_tables, (table, callback) =&gt;
          @_getSchema table, (error, schema) -&gt;
            return callback error if error
            table_schemas[table] = schema
            callback null
        , (error) -&gt;
          return callback error if error
          callback null, table_schemas
      ]
      get_indexes: (callback) =&gt;
        @_getIndexes callback
      get_foreign_keys: (callback) =&gt;
        @_getForeignKeys callback
    , (error, results) -&gt;
      callback error, tables: results.get_table_schemas, indexes: results.get_indexes, foreign_keys: results.get_foreign_keys

  ## @override AdapterBase::createTable
  createTable: (model, callback) -&gt;
    model_class = @_connection.models[model]
    tableName = model_class.tableName
    sql = []
    sql.push 'id INT NOT NULL AUTO_INCREMENT UNIQUE PRIMARY KEY'
    for column, property of model_class._schema
      column_sql = _propertyToSQL property, @support_fractional_seconds
      if column_sql
        sql.push &quot;`#{property._dbname}` #{column_sql}&quot;
    sql = &quot;CREATE TABLE `#{tableName}` ( #{sql.join ','} )&quot;
    sql += &quot; DEFAULT CHARSET=#{@_settings.charset or 'utf8'}&quot;
    sql += &quot; COLLATE=#{@_settings.collation or 'utf8_unicode_ci'}&quot;
    @_query sql, (error) -&gt;
      return callback MySQLAdapter.wrapError 'unknown error', error if error
      callback null

  ## @override AdapterBase::addColumn
  addColumn: (model, column_property, callback) -&gt;
    model_class = @_connection.models[model]
    tableName = model_class.tableName
    sql = &quot;ALTER TABLE `#{tableName}` ADD COLUMN `#{column_property._dbname}` #{_propertyToSQL column_property, @support_fractional_seconds}&quot;
    @_query sql, (error) -&gt;
      return callback MySQLAdapter.wrapError 'unknown error', error if error
      callback null

  ## @override AdapterBase::createIndex
  createIndex: (model, index, callback) -&gt;
    model_class = @_connection.models[model]
    tableName = model_class.tableName
    columns = []
    for column, order of index.columns
      columns.push &quot;`#{column}` #{if order is -1 then 'DESC' else 'ASC'}&quot;
    unique = if index.options.unique then 'UNIQUE ' else ''
    sql = &quot;CREATE #{unique}INDEX `#{index.options.name}` ON `#{tableName}` (#{columns.join ','})&quot;
    @_query sql, (error) -&gt;
      return callback MySQLAdapter.wrapError 'unknown error', error if error
      callback null

  ## @override AdapterBase::createForeignKey
  createForeignKey: (model, column, type, references, callback) -&gt;
    model_class = @_connection.models[model]
    tableName = model_class.tableName
    action = switch type
      when 'nullify' then 'SET NULL'
      when 'restrict' then 'RESTRICT'
      when 'delete' then 'CASCADE'
    sql = &quot;ALTER TABLE `#{tableName}` ADD FOREIGN KEY (`#{column}`) REFERENCES `#{references.tableName}`(id) ON DELETE #{action}&quot;
    @_query sql, (error) -&gt;
      if error
        return callback MySQLAdapter.wrapError 'unknown error', error
      callback null

  ## @override AdapterBase::drop
  drop: (model, callback) -&gt;
    tableName = @_connection.models[model].tableName
    @_query &quot;DROP TABLE IF EXISTS `#{tableName}`&quot;, (error) -&gt;
      return callback MySQLAdapter.wrapError 'unknown error', error if error
      callback null

  _getModelID: (data) -&gt;
    Number data.id

  valueToModel: (value, property) -&gt;
    if property.type_class is types.Object or property.array
      try
        JSON.parse value
      catch
        null
    else if property.type_class is types.GeoPoint
      [value.x, value.y]
    else if property.type_class is types.Boolean
      value isnt 0
    else
      value

  _processSaveError = (error, callback) -&gt;
    if error.code is 'ER_NO_SUCH_TABLE'
      error = new Error('table does not exist')
    else if error.code is 'ER_DUP_ENTRY'
      key = error.message.match /for key '([^']*)'/
      error = new Error('duplicated ' + key?[1])
    else if error.code is 'ER_BAD_NULL_ERROR'
      key = error.message.match /Column '([^']*)'/
      error = new Error(&quot;'#{key?[1]}' is required&quot;)
    else
      error = MySQLAdapter.wrapError 'unknown error', error
    callback error

  _buildUpdateSetOfColumn: (property, data, values, fields, places, insert) -&gt;
    dbname = property._dbname
    value = data[dbname]
    if property.type_class is types.GeoPoint
      values.push value[0]
      values.push value[1]
      if insert
        fields.push &quot;`#{dbname}`&quot;
        places.push 'POINT(?,?)'
      else
        fields.push &quot;`#{dbname}`=POINT(?,?)&quot;
    else if value?.$inc?
      values.push value.$inc
      fields.push &quot;`#{dbname}`=`#{dbname}`+?&quot;
    else
      values.push value
      if insert
        fields.push &quot;`#{dbname}`&quot;
        places.push '?'
      else
        fields.push &quot;`#{dbname}`=?&quot;

  _buildUpdateSet: (model, data, values, insert) -&gt;
    schema = @_connection.models[model]._schema
    fields = []
    places = []
    for column, property of schema
      @_buildUpdateSetOfColumn property, data, values, fields, places, insert
    [ fields.join(','), places.join(',') ]

  _buildPartialUpdateSet: (model, data, values) -&gt;
    schema = @_connection.models[model]._schema
    fields = []
    places = []
    for column, value of data
      property = _.find schema, (item) -&gt; return item._dbname is column
      @_buildUpdateSetOfColumn property, data, values, fields, places
    [ fields.join(','), places.join(',') ]

  ## @override AdapterBase::create
  create: (model, data, callback) -&gt;
    tableName = @_connection.models[model].tableName
    values = []
    [ fields, places ] = @_buildUpdateSet model, data, values, true
    sql = &quot;INSERT INTO `#{tableName}` (#{fields}) VALUES (#{places})&quot;
    @_query sql, values, (error, result) -&gt;
      return _processSaveError error, callback if error
      if id = result?.insertId
        callback null, id
      else
        callback new Error 'unexpected result'

  ## @override AdapterBase::createBulk
  createBulk: (model, data, callback) -&gt;
    tableName = @_connection.models[model].tableName
    values = []
    fields = undefined
    places = []
    data.forEach (item) =&gt;
      [ fields, places_sub ] = @_buildUpdateSet model, item, values, true
      places.push '(' + places_sub + ')'
    sql = &quot;INSERT INTO `#{tableName}` (#{fields}) VALUES #{places.join ','}&quot;
    @_query sql, values, (error, result) -&gt;
      return _processSaveError error, callback if error
      if id = result?.insertId
        callback null, data.map (item, i) -&gt; id + i
      else
        callback new Error 'unexpected result'

  ## @override AdapterBase::update
  update: (model, data, callback) -&gt;
    tableName = @_connection.models[model].tableName
    values = []
    [ fields ] = @_buildUpdateSet model, data, values
    values.push data.id
    sql = &quot;UPDATE `#{tableName}` SET #{fields} WHERE id=?&quot;
    @_query sql, values, (error) -&gt;
      return _processSaveError error, callback if error
      callback null

  ## @override AdapterBase::updatePartial
  updatePartial: (model, data, conditions, options, callback) -&gt;
    tableName = @_connection.models[model].tableName
    values = []
    [ fields ] = @_buildPartialUpdateSet model, data, values
    sql = &quot;UPDATE `#{tableName}` SET #{fields}&quot;
    if conditions.length &gt; 0
      try
        sql += ' WHERE ' + @_buildWhere @_connection.models[model]._schema, conditions, values
      catch e
        return callback e
    @_query sql, values, (error, result) -&gt;
      return _processSaveError error, callback if error
      return callback MySQLAdapter.wrapError 'unknown error' if not result?
      callback null, result.affectedRows

  ## @override AdapterBase::upsert
  upsert: (model, data, conditions, options, callback) -&gt;
    tableName = @_connection.models[model].tableName

    insert_data = {}
    for key, value of data
      if value?.$inc?
        insert_data[key] = value.$inc
      else
        insert_data[key] = value
    for condition in conditions
      for key, value of condition
        insert_data[key] = value
    values = []
    [ fields, places ] = @_buildUpdateSet model, insert_data, values, true
    sql = &quot;INSERT INTO `#{tableName}` (#{fields}) VALUES (#{places})&quot;

    [ fields ] = @_buildPartialUpdateSet model, data, values
    sql += &quot; ON DUPLICATE KEY UPDATE #{fields}&quot;

    @_query sql, values, (error, result) -&gt;
      return _processSaveError error, callback if error
      callback null

  ## @override AdapterBase::findById
  findById: (model, id, options, callback) -&gt;
    id = @_convertValueType id, @key_type
    select = @_buildSelect @_connection.models[model], options.select
    tableName = @_connection.models[model].tableName
    sql = &quot;SELECT #{select} FROM `#{tableName}` WHERE id=? LIMIT 1&quot;
    if options.explain
      return @_query &quot;EXPLAIN #{sql}&quot;, id, (error, result) -&gt;
        return callback error if error
        callback null, result
    @_query sql, id, (error, result) =&gt;
      return callback MySQLAdapter.wrapError 'unknown error', error if error
      if result?.length is 1
        callback null, @_convertToModelInstance model, result[0], options
      else if result?.length &gt; 1
        callback new Error 'unknown error'
      else
        callback new Error 'not found'

  _buildSqlForFind: (model, conditions, options) -&gt;
    if options.group_by or options.group_fields
      select = @_buildGroupFields options.group_by, options.group_fields
    else
      select = @_buildSelect @_connection.models[model], options.select
    if options.near? and field = Object.keys(options.near)[0]
      order_by = &quot;`#{field}_distance`&quot;
      location = options.near[field]
      select += &quot;,GLENGTH(LINESTRING(`#{field}`,POINT(#{location[0]},#{location[1]}))) AS `#{field}_distance`&quot;
    params = []
    tableName = @_connection.models[model].tableName
    sql = &quot;SELECT #{select} FROM `#{tableName}`&quot;
    if conditions.length &gt; 0
      sql += ' WHERE ' + @_buildWhere @_connection.models[model]._schema, conditions, params
    if options.group_by
      sql += ' GROUP BY ' + options.group_by.join ','
    if options.conditions_of_group.length &gt; 0
      sql += ' HAVING ' + @_buildWhere options.group_fields, options.conditions_of_group, params
    if options?.orders.length &gt; 0 or order_by
      model_class = @_connection.models[model]
      schema = model_class._schema
      orders = options.orders.map (order) -&gt;
        if order[0] is '-'
          column = order[1..]
          order = 'DESC'
        else
          column = order
          order = 'ASC'
        column = schema[column]?._dbname or column
        return &quot;`#{column}` #{order}&quot;
      if order_by
        orders.push order_by
      sql += ' ORDER BY ' + orders.join ','
    if options?.limit?
      sql += ' LIMIT ' + options.limit
      sql += ' OFFSET ' + options.skip if options?.skip?
    else if options?.skip?
      sql += ' LIMIT 2147483647 OFFSET ' + options.skip
    #console.log sql, params
    [sql, params]

  ## @override AdapterBase::find
  find: (model, conditions, options, callback) -&gt;
    try
      [sql, params] = @_buildSqlForFind model, conditions, options
    catch e
      return callback e
    if options.explain
      return @_query &quot;EXPLAIN #{sql}&quot;, params, (error, result) -&gt;
        return callback error if error
        callback null, result
    @_query sql, params, (error, result) =&gt;
      #console.log result
      return callback MySQLAdapter.wrapError 'unknown error', error if error
      if options.group_fields
        callback null, result.map (record) =&gt; @_convertToGroupInstance model, record, options.group_by, options.group_fields
      else
        callback null, result.map (record) =&gt; @_convertToModelInstance model, record, options

  ## @override AdapterBase::stream
  stream: (model, conditions, options) -&gt;
    try
      [sql, params] = @_buildSqlForFind model, conditions, options
    catch e
      readable = new stream.Readable objectMode: true
      readable._read = -&gt;
        readable.emit 'error', e
      return readable
    transformer = new stream.Transform objectMode: true
    transformer._transform = (record, encoding, callback) =&gt;
      transformer.push @_convertToModelInstance model, record, options
      callback()
    @_query(sql, params).stream()
    .on 'error', (error) -&gt;
      transformer.emit 'error', error
    .pipe transformer
    transformer

  ## @override AdapterBase::count
  count: (model, conditions, options, callback) -&gt;
    params = []
    tableName = @_connection.models[model].tableName
    sql = &quot;SELECT COUNT(*) AS count FROM `#{tableName}`&quot;
    if conditions.length &gt; 0
      try
        sql += ' WHERE ' + @_buildWhere @_connection.models[model]._schema, conditions, params
      catch e
        return callback e
    if options.group_by
      sql += ' GROUP BY ' + options.group_by.join ','
      if options.conditions_of_group.length &gt; 0
        try
          sql += ' HAVING ' + @_buildWhere options.group_fields, options.conditions_of_group, params
        catch e
          return callback e
      sql = &quot;SELECT COUNT(*) AS count FROM (#{sql}) _sub&quot;
    #console.log sql, params
    @_query sql, params, (error, result) =&gt;
      return callback MySQLAdapter.wrapError 'unknown error', error if error
      return callback new Error 'unknown error' if result?.length isnt 1
      callback null, Number(result[0].count)

  ## @override AdapterBase::delete
  delete: (model, conditions, callback) -&gt;
    params = []
    tableName = @_connection.models[model].tableName
    sql = &quot;DELETE FROM `#{tableName}`&quot;
    if conditions.length &gt; 0
      try
        sql += ' WHERE ' + @_buildWhere @_connection.models[model]._schema, conditions, params
      catch e
        return callback e
    #console.log sql, params
    @_query sql, params, (error, result) -&gt;
      return callback new Error 'rejected' if error and error.code in ['ER_ROW_IS_REFERENCED_', 'ER_ROW_IS_REFERENCED_2']
      return callback MySQLAdapter.wrapError 'unknown error', error if error or not result?
      callback null, result.affectedRows

  ##
  # Connects to the database
  # @param {Object} settings
  # @param {String} [settings.host]
  # @param {Number} [settings.port]
  # @param {String} [settings.user]
  # @param {String} [settings.password]
  # @param {String} settings.database
  # @param {String} [settings.charset='utf8']
  # @param {String} [settings.collation='utf8_unicode_ci']
  # @param {Number} [settings.pool_size=10]
  # @nodejscallback
  connect: (settings, callback) -&gt;
    # connect
    client = mysql.createConnection
      host: settings.host
      port: settings.port
      user: settings.user
      password: settings.password
      charset: settings.charset
    @_database = settings.database
    @_settings = settings
    client.connect (error) =&gt;
      if error
        client.end()
        return callback MySQLAdapter.wrapError 'failed to connect', error
      @_createDatabase client, (error) =&gt;
        if error
          client.end()
          return callback error
        @_checkFeatures client, (error) =&gt;
          client.end()
          return callback error if error
          @_client = mysql.createPool
            host: settings.host
            port: settings.port
            user: settings.user
            password: settings.password
            charset: settings.charset
            database: settings.database
            connectionLimit: settings.pool_size or 10
          callback null

  # create database if not exist
  _createDatabase: (client, callback) -&gt;
    # check database existence
    client.query &quot;USE `#{@_database}`&quot;, (error) =&gt;
      return callback null if not error

      if error.code is 'ER_BAD_DB_ERROR'
        client.query &quot;CREATE DATABASE `#{@_database}`&quot;, (error) =&gt;
          return callback MySQLAdapter.wrapError 'unknown error', error if error
          @_createDatabase client, callback
      else
        msg = if error.code is 'ER_DBACCESS_DENIED_ERROR' then &quot;no access right to the database '#{@_database}'&quot; else 'unknown error'
        callback MySQLAdapter.wrapError msg, error

  _checkFeatures: (client, callback) -&gt;
    client.query &quot;CREATE TABLE _temp (date DATETIME(10))&quot;, (error) =&gt;
      if error.code is 'ER_PARSE_ERROR'
        # MySQL 5.6.4 below does not support fractional seconds
        @support_fractional_seconds = false
      else if error.code is 'ER_TOO_BIG_PRECISION'
        @support_fractional_seconds = true
      else
        return callback error
      callback null

  ## @override AdapterBase::close
  close: -&gt;
    if @_client
      @_client.end()
    @_client = null

  ##
  # Exposes mysql module's query method
  query: -&gt;
    @_client.query.apply @_client, arguments

module.exports = (connection) -&gt;
  new MySQLAdapter connection</pre></div></div></div><script src="http://code.jquery.com/jquery-1.11.0.min.js"></script><script src="../bootstrap-3.2.0-dist/js/bootstrap.min.js"></script><script src="../google-code-prettify/prettify.js"></script><script src="../script.js"></script><script src="../group-examples.js"></script><a href="https://github.com/croquiscom/cormo"><img class="github-ribbon" src="https://s3.amazonaws.com/github/ribbons/forkme_right_green_007200.png" alt="Fork me on GitHub"></a></body></html>