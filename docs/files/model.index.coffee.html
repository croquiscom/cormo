<!DOCTYPE html><html lang="en"><head><meta charset="utf-8"><meta http-equiv="X-UA-Compatible" content="IE=edge"><meta name="viewport" content="width=device-width, initial-scale=1.0, maximum-scale=1.0, user-scalable=no"><title>CORMO - model/index.coffee</title><script>if (location.protocol.match(/^http/) && location.pathname.match('\.html') === null && location.pathname.slice(-1) !== '/') {
  location.href = location.href + '/';
}</script><link href="../bootstrap-3.2.0-dist/css/bootstrap.min.css" rel="stylesheet" type="text/css"><!--[if lt IE 9]><script src="https://oss.maxcdn.com/html5shiv/3.7.2/html5shiv.min.js"></script><script src="https://oss.maxcdn.com/respond/1.4.2/respond.min.js"></script><![endif]--><link href="../google-code-prettify/prettify.css" rel="stylesheet" type="text/css"><link href="../style.css" rel="stylesheet" type="text/css"></head><body data-spy="scroll" data-target=".sidebar"><nav class="navbar navbar-default navbar-fixed-top" role="navigation"><div class="navbar-header"><button class="navbar-toggle collapsed" type="button" data-toggle="collapse" data-target="#top-navigation-collapse"><span class="sr-only">Toggle navigation</span><span class="icon-bar"></span><span class="icon-bar"></span><span class="icon-bar"></span></button><div class="collapse navbar-collapse" id="top-navigation-collapse"><ul class="nav navbar-nav"><li><a href="../index.html">Home</a></li><li class="dropdown"><a class="dropdown-toggle" data-toggle="dropdown" href="#">Guides <span class="caret"></span></a><ul class="dropdown-menu"><li><a href="../guides/Aggregation.html">Aggregation</a></li><li><a href="../guides/Association.html">Association</a></li><li><a href="../guides/Callback.html">Callback</a></li><li><a href="../guides/Constraint.html">Constraint</a></li><li><a href="../guides/CreateRecords.html">Create records</a></li><li><a href="../guides/DefineModels.html">Define models</a></li><li><a href="../guides/Geospatial.html">Geospatial</a></li><li><a href="../guides/Miscellaneous.html">Miscellaneous</a></li><li><a href="../guides/Query.html">Query</a></li><li><a href="../guides/Validation.html">Validation</a></li></ul></li><li><a href="../modules/index.html">Modules</a></li><li><a href="../classes/index.html">Classes</a></li><li class="dropdown active"><a class="dropdown-toggle" data-toggle="dropdown" href="#">Files - model/index.coffee <span class="caret"></span></a><ul class="dropdown-menu"><li><a href="../files/index.coffee.html">index.coffee</a></li><li><a href="../files/query.coffee.html">query.coffee</a></li><li><a href="../files/types.coffee.html">types.coffee</a></li><li><a href="../files/adapters.base.coffee.html">adapters/base.coffee</a></li><li><a href="../files/adapters.mongodb.coffee.html">adapters/mongodb.coffee</a></li><li><a href="../files/adapters.mysql.coffee.html">adapters/mysql.coffee</a></li><li><a href="../files/adapters.postgresql.coffee.html">adapters/postgresql.coffee</a></li><li><a href="../files/adapters.redis.coffee.html">adapters/redis.coffee</a></li><li><a href="../files/adapters.sql_base.coffee.html">adapters/sql_base.coffee</a></li><li><a href="../files/adapters.sqlite3.coffee.html">adapters/sqlite3.coffee</a></li><li><a href="../files/adapters.sqlite3_memory.coffee.html">adapters/sqlite3_memory.coffee</a></li><li><a href="../files/command.index.coffee.html">command/index.coffee</a></li><li><a href="../files/command.remote-console.coffee.html">command/remote-console.coffee</a></li><li><a href="../files/connection.association.coffee.html">connection/association.coffee</a></li><li><a href="../files/connection.index.coffee.html">connection/index.coffee</a></li><li><a href="../files/connection.manipulate.coffee.html">connection/manipulate.coffee</a></li><li><a href="../files/model.cache.coffee.html">model/cache.coffee</a></li><li><a href="../files/model.callback.coffee.html">model/callback.coffee</a></li><li><a href="../files/model.index.coffee.html">model/index.coffee</a></li><li><a href="../files/model.persistence.coffee.html">model/persistence.coffee</a></li><li><a href="../files/model.query.coffee.html">model/query.coffee</a></li><li><a href="../files/model.timestamp.coffee.html">model/timestamp.coffee</a></li><li><a href="../files/model.validate.coffee.html">model/validate.coffee</a></li><li><a href="../files/util.index.coffee.html">util/index.coffee</a></li><li><a href="../files/util.inflector.coffee.html">util/inflector.coffee</a></li></ul></li></ul><div class="options"><label class="checkbox"><input id="options-private" type="checkbox"> Private </label></div></div></div></nav><div class="container-fluid content"><div class="row"><div class="hidden-xs sidebar col-sm-3" data-spy="affix"><div class="cormo-sidenav"><div class="panel panel-default"><div class="panel-collapse collapse in" id="undefined_body"><ul class="nav nav-pills nav-stacked"><li><a href="../files/index.coffee.html">index.coffee</a></li><li><a href="../files/query.coffee.html">query.coffee</a></li><li><a href="../files/types.coffee.html">types.coffee</a></li></ul></div></div><div class="panel panel-default"><div class="panel-heading" data-toggle="collapse" data-target="#adapters__body">adapters/<span class="pull-right glyphicon"></span></div><div class="panel-collapse collapse in" id="adapters__body"><ul class="nav nav-pills nav-stacked"><li><a href="../files/adapters.base.coffee.html">base.coffee</a></li><li><a href="../files/adapters.mongodb.coffee.html">mongodb.coffee</a></li><li><a href="../files/adapters.mysql.coffee.html">mysql.coffee</a></li><li><a href="../files/adapters.postgresql.coffee.html">postgresql.coffee</a></li><li><a href="../files/adapters.redis.coffee.html">redis.coffee</a></li><li><a href="../files/adapters.sql_base.coffee.html">sql_base.coffee</a></li><li><a href="../files/adapters.sqlite3.coffee.html">sqlite3.coffee</a></li><li><a href="../files/adapters.sqlite3_memory.coffee.html">sqlite3_memory.coffee</a></li></ul></div></div><div class="panel panel-default"><div class="panel-heading" data-toggle="collapse" data-target="#command__body">command/<span class="pull-right glyphicon"></span></div><div class="panel-collapse collapse in" id="command__body"><ul class="nav nav-pills nav-stacked"><li><a href="../files/command.index.coffee.html">index.coffee</a></li><li><a href="../files/command.remote-console.coffee.html">remote-console.coffee</a></li></ul></div></div><div class="panel panel-default"><div class="panel-heading" data-toggle="collapse" data-target="#connection__body">connection/<span class="pull-right glyphicon"></span></div><div class="panel-collapse collapse in" id="connection__body"><ul class="nav nav-pills nav-stacked"><li><a href="../files/connection.association.coffee.html">association.coffee</a></li><li><a href="../files/connection.index.coffee.html">index.coffee</a></li><li><a href="../files/connection.manipulate.coffee.html">manipulate.coffee</a></li></ul></div></div><div class="panel panel-default"><div class="panel-heading" data-toggle="collapse" data-target="#model__body">model/<span class="pull-right glyphicon"></span></div><div class="panel-collapse collapse in" id="model__body"><ul class="nav nav-pills nav-stacked"><li><a href="../files/model.cache.coffee.html">cache.coffee</a></li><li><a href="../files/model.callback.coffee.html">callback.coffee</a></li><li class="active"><a href="../files/model.index.coffee.html">index.coffee</a></li><li><a href="../files/model.persistence.coffee.html">persistence.coffee</a></li><li><a href="../files/model.query.coffee.html">query.coffee</a></li><li><a href="../files/model.timestamp.coffee.html">timestamp.coffee</a></li><li><a href="../files/model.validate.coffee.html">validate.coffee</a></li></ul></div></div><div class="panel panel-default"><div class="panel-heading" data-toggle="collapse" data-target="#util__body">util/<span class="pull-right glyphicon"></span></div><div class="panel-collapse collapse in" id="util__body"><ul class="nav nav-pills nav-stacked"><li><a href="../files/util.index.coffee.html">index.coffee</a></li><li><a href="../files/util.inflector.coffee.html">inflector.coffee</a></li></ul></div></div></div></div><div class="col-sm-9 col-sm-offset-3"><section><h1>model/index.coffee</h1></section><pre class="prettyprint">_ = require 'lodash'
Promise = require 'bluebird'
tableize = require('../util/inflector').tableize
types = require '../types'
util = require '../util'

_pf_isDirty = -&gt; true
_pf_getChanged = -&gt; []
_pf_get = (path) -&gt; util.getPropertyOfPath @, path.split '.'
_pf_getPrevious = -&gt;
_pf_set = (path, value) -&gt; util.setPropertyOfPath @, path.split('.'), value
_pf_reset = -&gt;

##
# Properties of a column of a model
class ColumnProperty
  ##
  # @property type

  ##
  # @property required

  ##
  # @property unique

  ##
  # @property _parts
  # @private

  ##
  # Name for SQL dbs.
  # e.g.) name.first -&gt; name_first
  # @property _dbname
  # @private

##
# Base class for models
# @uses ModelQuery
# @uses ModelCallback
# @uses ModelTimestamp
# @uses ModelPersistence
# @uses ModelValidate
# @uses ModelCache
class Model
  ##
  # Tracks changes of a record if true
  # @type Boolean
  @dirty_tracking: false

  ##
  # Archives deleted records in the archive table
  @archive: false

  ##
  # Applies the lean option for all queries for this Model
  @lean_query: false

  ##
  # @property tableName
  # @type String
  # @static

  ##
  # Indicates the connection associated to this model
  # @property _connection
  # @type Connection
  # @private
  # @static
  # @see Model.connection

  ##
  # Indicates the adapter associated to this model
  # @property _adapter
  # @type AdapterBase
  # @private
  # @static
  # @see Model.connection

  ##
  # Schema for this model.
  # Maps from column path to property object
  # @property _schema
  # @type StringMap&lt;Object&gt;
  # @private
  # @static
  # @see Model.connection

  ##
  # Returns a new model class extending Model
  # @param {Connection} connection
  # @param {String} name
  # @param {Object} schema
  # @return {Class&lt;Model&gt;}
  @newModel: (connection, name, schema) -&gt;
    class NewModel extends Model
    NewModel.connection connection, name
    for name, property of schema
      NewModel.column name, property
    return NewModel

  ##
  # Sets a connection of this model
  #
  # If this methods was not called explicitly, this model will use Connection.defaultConnection
  # @param {Connection} connection
  # @param {String} [name]
  @connection: (connection, name) -&gt;
    if @hasOwnProperty '_connection'
      throw new Error 'Model::connection was called twice'

    name = @name if not name
    connection.models[name] = @
    connection[name] = @

    Object.defineProperty @, '_connection', value: connection
    Object.defineProperty @, '_adapter', value: connection._adapter
    Object.defineProperty @, '_associations', value: {}
    Object.defineProperty @, '_validators', value: []
    Object.defineProperty @, '_name', value: name
    Object.defineProperty @, '_schema', value: {}
    Object.defineProperty @, '_intermediate_paths', value: {}
    Object.defineProperty @, '_indexes', value: []
    Object.defineProperty @, '_integrities', value: []

    @tableName = tableize name if not @tableName

  @_checkConnection: -&gt;
    return if @hasOwnProperty '_connection'
    if not Model._Connection.defaultConnection?
      throw new Error 'Create a Connection before creating a Model'
    @connection Model._Connection.defaultConnection

  @_checkReady: -&gt;
    @_checkConnection()
    Promise.all [@_connection._checkSchemaApplied(), @_connection._promise_connection]

  @_getKeyType: (target_connection = @_connection) -&gt;
    if @_connection is target_connection and target_connection._adapter.key_type_internal
      new target_connection._adapter.key_type_internal
    else
      new target_connection._adapter.key_type

  ##
  # Adds a column to this model
  # @param {String} path
  # @param {Function|String|ColumnProperty} property
  @column: (path, property) -&gt;
    @_checkConnection()

    # nested path
    if _.isPlainObject(property) and (not property.type or property.type.type)
      for subcolumn, subproperty of property
        @column path+'.'+subcolumn, subproperty
      return

    if @_schema.hasOwnProperty path
      # if using association, a column may be defined more than twice (by hasMany and belongsTo, for example)
      # overwrite some properties if given later
      if property?.required?
        @_schema[path].required = property.required
      return

    # convert simple type to property object
    if not _.isPlainObject property
      property = type: property

    if Array.isArray property.type
      property.array = true
      property.type = property.type[0]

    type = types._toCORMOType property.type
    if type.constructor is types.RecordID
      type = @_getKeyType property.connection
      property.record_id = true

    # check supports of GeoPoint
    if type.constructor is types.GeoPoint and not @_adapter.support_geopoint
      throw new Error 'this adapter does not support GeoPoint type'
    if type.constructor is types.String and type.length and not @_adapter.support_string_type_with_length
      throw new Error 'this adapter does not support String type with length'

    parts = path.split '.'
    for i in [0...parts.length-1]
      @_intermediate_paths[parts[0..i].join '.'] = 1

    property.type = type
    property.type_class = type.constructor
    property._parts = path.split '.'
    property._dbname = path.replace /\./g, '_'

    @_schema[path] = property

    if property.unique
      @_indexes.push columns: _.zipObject([property._dbname], [1]), options: name: property._dbname, unique: true, required: property.required

    @_connection._schema_changed = true

  ##
  # Adds an index to this model
  # @param {Object} columns hash of &lt;column, order&gt;
  # @param {Object} [options]
  # @param {Boolean} [options.unique]
  @index: (columns, options) -&gt;
    @_checkConnection()

    options ||= {}
    if not options.name
      options.name = Object.keys(columns).join('_')
    @_indexes.push columns: columns, options: options

    @_connection._schema_changed = true

  ##
  # Drops this model from the database
  # @promise
  # @nodejscallback
  # @see AdapterBase::drop
  @drop: (callback) -&gt;
    # do not need to apply schema before drop, only waiting connection established
    @_connection._promise_connection.then =&gt;
      @_adapter.dropAsync @_name
    .finally =&gt;
      @_connection._schema_changed = true
    .nodeify util.bindDomain callback

  ##
  # Creates a record.
  # 'Model.build(data)' is the same as 'new Model(data)'
  # @param {Object} [data={}]
  # @return {Model}
  @build: (data) -&gt;
    return new @ data

  ##
  # @property _prev_attributes
  # @private

  ##
  # @property _attributes
  # @private

  ##
  # @property _intermediates
  # @private

  ##
  # Creates a record
  # @param {Object} [data={}]
  constructor: (data) -&gt;
    data = data or {}
    ctor = @constructor
    schema = ctor._schema
    adapter = ctor._adapter

    Object.defineProperty @, '_prev_attributes', writable: true, value: {}
    if ctor.dirty_tracking
      Object.defineProperty @, '_attributes', value: {}
      Object.defineProperty @, '_intermediates', value: {}
      for path in Object.keys(ctor._intermediate_paths).sort()
        [obj, last] = util.getLeafOfPath @, path
        @_intermediates[path] = {}
        @_defineProperty obj, last, path, false
      for column, property of schema
        [obj, last] = util.getLeafOfPath @, property._parts
        @_defineProperty obj, last, column, false
    else
      Object.defineProperty @, 'isDirty', value: _pf_isDirty
      Object.defineProperty @, 'getChanged', value: _pf_getChanged
      Object.defineProperty @, 'get', value: _pf_get
      Object.defineProperty @, 'getPrevious', value: _pf_getPrevious
      Object.defineProperty @, 'set', value: _pf_set
      Object.defineProperty @, 'reset', value: _pf_reset

    if id = arguments[1]
      # if id exists, this is called from adapter with database record data
      selected_columns = arguments[2]
      selected_columns_raw = arguments[3]
      adapter.setValuesFromDB @, data, schema, selected_columns

      ctor._collapseNestedNulls @, selected_columns_raw, if ctor.dirty_tracking then @_intermediates

      Object.defineProperty @, 'id', configurable: false, enumerable: true, writable: false, value: id

      @_runCallbacks 'find', 'after'
    else
      for column, property of schema
        parts = property._parts
        value = util.getPropertyOfPath data, parts
        if value is undefined
          value = null
        util.setPropertyOfPath @, parts, value

      ctor._collapseNestedNulls @, null, if ctor.dirty_tracking then @_intermediates

      Object.defineProperty @, 'id', configurable: true, enumerable: true, writable: false, value: null

    @_runCallbacks 'initialize', 'after'

  ##
  # Set nested object null if all children are null
  @_collapseNestedNulls: (instance, selected_columns_raw, intermediates) -&gt;
    for path in Object.keys(@_intermediate_paths)
      if selected_columns_raw and selected_columns_raw.indexOf(path) is -1
        continue
      if intermediates
        obj = intermediates
        last = path
      else
        [obj, last] = util.getLeafOfPath instance, path
      has_non_null = false
      for key, value of obj[last]
        has_non_null = true if value?
      if not has_non_null
        obj[last] = null

  _defineProperty: (object, key, path, enumerable) -&gt;
    Object.defineProperty object, key,
      configurable: true
      enumerable: enumerable
      get: =&gt; @get path
      set: (value) =&gt; @set path, value

  ##
  # Returns true if there is some changed columns
  isDirty: -&gt;
    Object.keys(@_prev_attributes).length &gt; 0

  ##
  # Returns the list of paths of changed columns
  getChanged: -&gt;
    Object.keys @_prev_attributes

  ##
  # Returns the current value of the column of the given path
  # @param {String} path
  # @return {*}
  get: (path) -&gt;
    if @_intermediates.hasOwnProperty path
      @_intermediates[path]
    else
      util.getPropertyOfPath @_attributes, path

  ##
  # Returns the original value of the column of the given path
  # @param {String} path
  # @return {*}
  getPrevious: (path) -&gt;
    @_prev_attributes[path]

  ##
  # Changes the value of the column of the given path
  # @param {String} path
  # @param {*} value
  # @return {*}
  set: (path, value) -&gt;
    if @_intermediates.hasOwnProperty path
      obj = @_intermediates[path]
      for k of obj
        obj[k] = undefined
      for k, v of value
        obj[k] = v
    else
      parts = path.split '.'
      prev_value = util.getPropertyOfPath @_attributes, parts
      return if prev_value is value
      if not @_prev_attributes.hasOwnProperty path
        @_prev_attributes[path] = prev_value
      [obj, last] = util.getLeafOfPath @, parts
      @_defineProperty obj, last, path, true
      util.setPropertyOfPath @_attributes, parts, value
      while parts.length &gt; 1
        parts.pop()
        [obj, last] = util.getLeafOfPath @, parts
        @_defineProperty obj, last, parts.join('.'), true

  ##
  # Resets all changes
  reset: -&gt;
    for path, value of @_prev_attributes
      @set path, value
    @_prev_attributes = {}

  ##
  # Destroys this record (remove from the database)
  # @promise
  # @nodejscallback
  destroy: (callback) -&gt;
    @_runCallbacks 'destroy', 'before'
    Promise.resolve()
    .then =&gt;
      if @id
        @constructor.delete id: @id
    .finally =&gt;
      @_runCallbacks 'destroy', 'after'
    .nodeify util.bindDomain callback

  ##
  # Deletes all records from the database
  # @promise
  # @nodejscallback
  @deleteAll: (callback) -&gt;
    @delete().nodeify callback

  ##
  # Adds a has-many association
  # @param {Class&lt;Model&gt;|String} target_model_or_column
  # @param {Object} [options]
  # @param {String} [options.type]
  # @param {String} [options.as]
  # @param {String} [options.foreign_key]
  # @param {String} [options.integrity='ignore'] 'ignore', 'nullify', 'restrict', or 'delete'
  @hasMany: (target_model_or_column, options) -&gt;
    @_checkConnection()

    @_connection.addAssociation
      type: 'hasMany'
      this_model: @
      target_model_or_column: target_model_or_column
      options: options

  ##
  # Adds a has-one association
  # @param {Class&lt;Model&gt;|String} target_model_or_column
  # @param {Object} [options]
  # @param {String} [options.type]
  # @param {String} [options.as]
  # @param {String} [options.foreign_key]
  # @param {String} [options.integrity='ignore'] 'ignore', 'nullify', 'restrict', or 'delete'
  @hasOne: (target_model_or_column, options) -&gt;
    @_checkConnection()

    @_connection.addAssociation
      type: 'hasOne'
      this_model: @
      target_model_or_column: target_model_or_column
      options: options

  ##
  # Adds a belongs-to association
  # @param {Class&lt;Model&gt;|String} target_model_or_column
  # @param {Object} [options]
  # @param {String} [options.type]
  # @param {String} [options.as]
  # @param {String} [options.foreign_key]
  # @param {Boolean} [options.required]
  @belongsTo: (target_model_or_column, options) -&gt;
    @_checkConnection()

    @_connection.addAssociation
      type: 'belongsTo'
      this_model: @
      target_model_or_column: target_model_or_column
      options: options

  @inspect: (depth) -&gt;
    schema = Object.keys(@_schema or {}).sort().map((column) =&gt; return &quot;#{column}: #{@_schema[column].type}&quot;).join(', ')
    return '\u001b[36m' + &quot;[Model: #{@name}(&quot; + '\u001b[90m' + schema + '\u001b[36m' + &quot;)]&quot; + '\u001b[39m'

_use = (file) -&gt;
  MixClass = require &quot;./#{file}&quot;
  _.extend Model, MixClass
  _.extend Model::, MixClass::
_use 'query'
_use 'callback'
_use 'timestamp'
_use 'persistence'
_use 'validate'
_use 'cache'

module.exports = Model</pre></div></div></div><script src="http://code.jquery.com/jquery-1.11.0.min.js"></script><script src="../bootstrap-3.2.0-dist/js/bootstrap.min.js"></script><script src="../google-code-prettify/prettify.js"></script><script src="../script.js"></script><script src="../group-examples.js"></script><a href="https://github.com/croquiscom/cormo"><img class="github-ribbon" src="https://s3.amazonaws.com/github/ribbons/forkme_right_green_007200.png" alt="Fork me on GitHub"></a></body></html>