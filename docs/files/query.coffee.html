<!DOCTYPE html><html lang="en"><head><meta charset="utf-8"><meta http-equiv="X-UA-Compatible" content="IE=edge"><meta name="viewport" content="width=device-width, initial-scale=1.0, maximum-scale=1.0, user-scalable=no"><title>CORMO - query.coffee</title><script>if (location.protocol.match(/^http/) && location.pathname.match('\.html') === null && location.pathname.slice(-1) !== '/') {
  location.href = location.href + '/';
}</script><link href="../bootstrap-3.2.0-dist/css/bootstrap.min.css" rel="stylesheet" type="text/css"><!--[if lt IE 9]><script src="https://oss.maxcdn.com/html5shiv/3.7.2/html5shiv.min.js"></script><script src="https://oss.maxcdn.com/respond/1.4.2/respond.min.js"></script><![endif]--><link href="../google-code-prettify/prettify.css" rel="stylesheet" type="text/css"><link href="../style.css" rel="stylesheet" type="text/css"></head><body data-spy="scroll" data-target=".sidebar"><nav class="navbar navbar-default navbar-fixed-top" role="navigation"><div class="navbar-header"><button class="navbar-toggle collapsed" type="button" data-toggle="collapse" data-target="#top-navigation-collapse"><span class="sr-only">Toggle navigation</span><span class="icon-bar"></span><span class="icon-bar"></span><span class="icon-bar"></span></button><div class="collapse navbar-collapse" id="top-navigation-collapse"><ul class="nav navbar-nav"><li><a href="../index.html">Home</a></li><li class="dropdown"><a class="dropdown-toggle" data-toggle="dropdown" href="#">Guides <span class="caret"></span></a><ul class="dropdown-menu"><li><a href="../guides/Aggregation.html">Aggregation</a></li><li><a href="../guides/Association.html">Association</a></li><li><a href="../guides/Callback.html">Callback</a></li><li><a href="../guides/Constraint.html">Constraint</a></li><li><a href="../guides/CreateRecords.html">Create records</a></li><li><a href="../guides/DefineModels.html">Define models</a></li><li><a href="../guides/Geospatial.html">Geospatial</a></li><li><a href="../guides/Miscellaneous.html">Miscellaneous</a></li><li><a href="../guides/Query.html">Query</a></li><li><a href="../guides/Validation.html">Validation</a></li></ul></li><li><a href="../modules/index.html">Modules</a></li><li><a href="../classes/index.html">Classes</a></li><li class="dropdown active"><a class="dropdown-toggle" data-toggle="dropdown" href="#">Files - query.coffee <span class="caret"></span></a><ul class="dropdown-menu"><li><a href="../files/index.coffee.html">index.coffee</a></li><li><a href="../files/query.coffee.html">query.coffee</a></li><li><a href="../files/types.coffee.html">types.coffee</a></li><li><a href="../files/adapters.base.coffee.html">adapters/base.coffee</a></li><li><a href="../files/adapters.mongodb.coffee.html">adapters/mongodb.coffee</a></li><li><a href="../files/adapters.mysql.coffee.html">adapters/mysql.coffee</a></li><li><a href="../files/adapters.postgresql.coffee.html">adapters/postgresql.coffee</a></li><li><a href="../files/adapters.redis.coffee.html">adapters/redis.coffee</a></li><li><a href="../files/adapters.sql_base.coffee.html">adapters/sql_base.coffee</a></li><li><a href="../files/adapters.sqlite3.coffee.html">adapters/sqlite3.coffee</a></li><li><a href="../files/adapters.sqlite3_memory.coffee.html">adapters/sqlite3_memory.coffee</a></li><li><a href="../files/command.index.coffee.html">command/index.coffee</a></li><li><a href="../files/command.remote-console.coffee.html">command/remote-console.coffee</a></li><li><a href="../files/connection.association.coffee.html">connection/association.coffee</a></li><li><a href="../files/connection.index.coffee.html">connection/index.coffee</a></li><li><a href="../files/connection.manipulate.coffee.html">connection/manipulate.coffee</a></li><li><a href="../files/model.cache.coffee.html">model/cache.coffee</a></li><li><a href="../files/model.callback.coffee.html">model/callback.coffee</a></li><li><a href="../files/model.index.coffee.html">model/index.coffee</a></li><li><a href="../files/model.persistence.coffee.html">model/persistence.coffee</a></li><li><a href="../files/model.query.coffee.html">model/query.coffee</a></li><li><a href="../files/model.timestamp.coffee.html">model/timestamp.coffee</a></li><li><a href="../files/model.validate.coffee.html">model/validate.coffee</a></li><li><a href="../files/util.index.coffee.html">util/index.coffee</a></li><li><a href="../files/util.inflector.coffee.html">util/inflector.coffee</a></li></ul></li></ul><div class="options"><label class="checkbox"><input id="options-private" type="checkbox"> Private </label></div></div></div></nav><div class="container-fluid content"><div class="row"><div class="hidden-xs sidebar col-sm-3" data-spy="affix"><div class="cormo-sidenav"><div class="panel panel-default"><div class="panel-collapse collapse in" id="undefined_body"><ul class="nav nav-pills nav-stacked"><li><a href="../files/index.coffee.html">index.coffee</a></li><li class="active"><a href="../files/query.coffee.html">query.coffee</a></li><li><a href="../files/types.coffee.html">types.coffee</a></li></ul></div></div><div class="panel panel-default"><div class="panel-heading" data-toggle="collapse" data-target="#adapters__body">adapters/<span class="pull-right glyphicon"></span></div><div class="panel-collapse collapse in" id="adapters__body"><ul class="nav nav-pills nav-stacked"><li><a href="../files/adapters.base.coffee.html">base.coffee</a></li><li><a href="../files/adapters.mongodb.coffee.html">mongodb.coffee</a></li><li><a href="../files/adapters.mysql.coffee.html">mysql.coffee</a></li><li><a href="../files/adapters.postgresql.coffee.html">postgresql.coffee</a></li><li><a href="../files/adapters.redis.coffee.html">redis.coffee</a></li><li><a href="../files/adapters.sql_base.coffee.html">sql_base.coffee</a></li><li><a href="../files/adapters.sqlite3.coffee.html">sqlite3.coffee</a></li><li><a href="../files/adapters.sqlite3_memory.coffee.html">sqlite3_memory.coffee</a></li></ul></div></div><div class="panel panel-default"><div class="panel-heading" data-toggle="collapse" data-target="#command__body">command/<span class="pull-right glyphicon"></span></div><div class="panel-collapse collapse in" id="command__body"><ul class="nav nav-pills nav-stacked"><li><a href="../files/command.index.coffee.html">index.coffee</a></li><li><a href="../files/command.remote-console.coffee.html">remote-console.coffee</a></li></ul></div></div><div class="panel panel-default"><div class="panel-heading" data-toggle="collapse" data-target="#connection__body">connection/<span class="pull-right glyphicon"></span></div><div class="panel-collapse collapse in" id="connection__body"><ul class="nav nav-pills nav-stacked"><li><a href="../files/connection.association.coffee.html">association.coffee</a></li><li><a href="../files/connection.index.coffee.html">index.coffee</a></li><li><a href="../files/connection.manipulate.coffee.html">manipulate.coffee</a></li></ul></div></div><div class="panel panel-default"><div class="panel-heading" data-toggle="collapse" data-target="#model__body">model/<span class="pull-right glyphicon"></span></div><div class="panel-collapse collapse in" id="model__body"><ul class="nav nav-pills nav-stacked"><li><a href="../files/model.cache.coffee.html">cache.coffee</a></li><li><a href="../files/model.callback.coffee.html">callback.coffee</a></li><li><a href="../files/model.index.coffee.html">index.coffee</a></li><li><a href="../files/model.persistence.coffee.html">persistence.coffee</a></li><li><a href="../files/model.query.coffee.html">query.coffee</a></li><li><a href="../files/model.timestamp.coffee.html">timestamp.coffee</a></li><li><a href="../files/model.validate.coffee.html">validate.coffee</a></li></ul></div></div><div class="panel panel-default"><div class="panel-heading" data-toggle="collapse" data-target="#util__body">util/<span class="pull-right glyphicon"></span></div><div class="panel-collapse collapse in" id="util__body"><ul class="nav nav-pills nav-stacked"><li><a href="../files/util.index.coffee.html">index.coffee</a></li><li><a href="../files/util.inflector.coffee.html">inflector.coffee</a></li></ul></div></div></div></div><div class="col-sm-9 col-sm-offset-3"><section><h1>query.coffee</h1></section><pre class="prettyprint">_ = require 'lodash'
{bindDomain} = require './util'
Promise = require 'bluebird'
stream = require 'stream'

##
# Collects conditions to query
class Query
  ##
  # Creates a query instance
  # @param {Class&lt;Model&gt;} model
  constructor: (model) -&gt;
    @_model = model
    @_name = model._name
    @_connection = model._connection
    @_adapter = model._connection._adapter
    @_ifs = []
    @_current_if = true
    @_conditions = []
    @_includes = []
    @_options =
      orders: []
      conditions_of_group: []
      lean: model.lean_query
 
  ##
  # Finds a record by id
  # @param {RecordID|Array&lt;RecordID&gt;} id
  # @chainable
  find: (id) -&gt;
    if not @_current_if
      return @
    if Array.isArray id
      @_id = _.uniq id
      @_find_single_id = false
    else
      @_id = id
      @_find_single_id = true
    return @
 
  ##
  # Finds records by ids while preserving order.
  # @param {Array&lt;RecordID&gt;} ids
  # @chainable
  findPreserve: (ids) -&gt;
    if not @_current_if
      return @
    @_id = _.uniq ids
    @_find_single_id = false
    @_preserve_order_ids = ids
    return @

  ##
  # Finds records near target
  # @param {Object} target
  # @chainable
  near: (target) -&gt;
    if not @_current_if
      return @
    @_options.near = target
    return @

  ##
  # @private
  _addCondition: (condition) -&gt;
    if @_options.group_fields
      keys = Object.keys condition
      if keys.length is 1 and @_options.group_fields.hasOwnProperty keys[0]
        @_options.conditions_of_group.push condition
        return
    @_conditions.push condition

  ##
  # Finds records by condition
  # @param {Object} condition
  # @chainable
  where: (condition) -&gt;
    if not @_current_if
      return @
    if Array.isArray condition
      condition.forEach (cond) =&gt;
        @_addCondition cond
    else if condition?
      @_addCondition condition
    return @

  ##
  # Selects columns for result
  # @param {String} columns
  # @chainable
  select: (columns) -&gt;
    if not @_current_if
      return @
    @_options.select = null
    @_options.select_raw = null
    if typeof columns is 'string'
      schema_columns = Object.keys @_model._schema
      intermediate_paths = @_model._intermediate_paths
      select = []
      select_raw = []
      columns.split(/\s+/).forEach (column) -&gt;
        if schema_columns.indexOf(column) &gt;= 0
          select.push column
          select_raw.push column
        else if intermediate_paths[column]
          # select all nested columns
          select_raw.push column
          column += '.'
          schema_columns.forEach (sc) -&gt;
            select.push sc if sc.indexOf(column) is 0
      @_options.select = select
      @_options.select_raw = select_raw
    return @

  ##
  # Specifies orders of result
  # @param {String} orders
  # @chainable
  order: (orders) -&gt;
    if not @_current_if
      return @
    if typeof orders is 'string'
      avaliable_columns = ['id']
      [].push.apply avaliable_columns, Object.keys @_model._schema
      [].push.apply avaliable_columns, Object.keys @_options.group_fields if @_options.group_fields
      orders.split(/\s+/).forEach (order) =&gt;
        asc = true
        if order[0] is '-'
          asc = false
          order = order[1..]
        if avaliable_columns.indexOf(order) &gt;= 0
          @_options.orders.push if asc then order else '-'+order
    return @

  ##
  # Groups result records
  # @param {String} group_by
  # @param {Object} fields
  # @chainable
  group: (group_by, fields) -&gt;
    if not @_current_if
      return @
    @_options.group_by = null
    schema_columns = Object.keys @_model._schema
    if typeof group_by is 'string'
      columns = group_by.split(/\s+/).filter (column) -&gt; schema_columns.indexOf(column) &gt;= 0
      @_options.group_by = columns
    @_options.group_fields = fields
    return @

  ##
  # Returns only one record (or null if does not exists).
  #
  # This is different from limit(1). limit(1) returns array of length 1 while this returns an instance.
  # @chainable
  one: -&gt;
    if not @_current_if
      return @
    @_options.limit = 1
    @_options.one = true
    return @

  ##
  # Sets limit of query
  # @param {Number} limit
  # @chainable
  limit: (limit) -&gt;
    if not @_current_if
      return @
    @_options.limit = limit
    return @

  ##
  # Sets skip of query
  # @param {Number} skip
  # @chainable
  skip: (skip) -&gt;
    if not @_current_if
      return @
    @_options.skip = skip
    return @

  ##
  # Returns raw instances instead of model instances
  # @param {Boolean} lean=true
  # @chainable
  # @see Query::exec
  lean: (lean=true) -&gt;
    if not @_current_if
      return @
    @_options.lean = lean
    return @

  ##
  # Makes a part of the query chain conditional
  # @param {Boolean} condition
  # @chainable
  # @see Query::endif
  if: (condition) -&gt;
    @_ifs.push condition
    @_current_if and= condition
    return @

  ##
  # Ends last if
  # @chainable
  # @see Query::if
  endif: -&gt;
    @_ifs.pop()
    @_current_if = true
    for condition in @_ifs
      @_current_if and= condition
    return @

  ##
  # Cache result.
  #
  # If cache of key exists, actual query does not performed.
  # If cache does not exist, query result will be saved in cache.
  #
  # Redis is used to cache.
  # @param {Object} options
  # @param {String} options.key
  # @param {Number} options.ttl TTL in seconds
  # @param {Boolean} options.refresh don't load from cache if true
  # @chainable
  cache: (options) -&gt;
    if not @_current_if
      return @
    @_options.cache = options
    return @

  ##
  # Returns associated objects also
  # @param {String} column
  # @param {String} [select]
  # @chainable
  include: (column, select) -&gt;
    if not @_current_if
      return @
    @_includes.push column: column, select: select
    return @

  ##
  # @private
  _exec: (options) -&gt;
    if @_find_single_id and @_conditions.length is 0
      @_connection.log @_name, 'find by id', id: @_id, options: @_options if not options?.skip_log
      return Promise.reject new Error('not found') if not @_id
      return @_adapter.findByIdAsync @_name, @_id, @_options
      .catch (error) -&gt;
        Promise.reject new Error('not found')
      .then (record) -&gt;
        return Promise.reject new Error('not found') if not record
        return record
    expected_count = undefined
    if @_id or @_find_single_id
      if Array.isArray @_id
        return Promise.resolve [] if @_id.length is 0
        @_conditions.push id: { $in: @_id }
        expected_count = @_id.length
      else
        @_conditions.push id: @_id
        expected_count = 1
    @_connection.log @_name, 'find', conditions: @_conditions, options: @_options if not options?.skip_log
    @_adapter.findAsync @_name, @_conditions, @_options
    .then (records) =&gt;
      if expected_count?
        return Promise.reject new Error('not found') if records.length isnt expected_count
      if @_preserve_order_ids
        records =  @_preserve_order_ids.map (id) -&gt;
          for record in records
            return record if record.id is id
      if @_options.one
        return Promise.reject new Error('unknown error') if records.length &gt; 1
        Promise.resolve if records.length is 1 then records[0] else null
      else
        Promise.resolve records

  ##
  # @private
  _execAndInclude: (options) -&gt;
    @_exec options
    .then (records) =&gt;
      promises = @_includes.map (include) =&gt;
        @_connection.fetchAssociated records, include.column, include.select, model: @_model, lean: @_options.lean
      Promise.all(promises)
      .then -&gt;
        records

  ##
  # Executes the query
  # @param {Object} [options]
  # @param {Boolean} [options.skip_log=false]
  # @return {Model|Array&lt;Model&gt;}
  # @promise
  # @nodejscallback
  # @see AdapterBase::findById
  # @see AdapterBase::find
  exec: (options, callback) -&gt;
    if typeof options is 'function'
      callback = options
      options = {}

    @_model._checkReady().then =&gt;
      if (cache_options = @_options.cache) and (cache_key = cache_options.key)
        # try cache
        @_model._loadFromCache cache_key, cache_options.refresh
        .catch (error) =&gt;
          # no cache, execute query
          @_execAndInclude options
          .then (records) =&gt;
            # save result to cache
            @_model._saveToCache cache_key, cache_options.ttl, records
            .then -&gt;
              records
      else
        @_execAndInclude options
    .nodeify bindDomain callback

  ##
  # Executes the query and returns a readable stream
  # @param {Object} [options]
  # @param {Boolean} [options.skip_log=false]
  # @return {Readable}
  # @see AdapterBase::findById
  # @see AdapterBase::find
  stream: -&gt;
    transformer = new stream.Transform objectMode: true
    transformer._transform = (chunk, encoding, callback) -&gt;
      @push chunk
      callback()
    @_model._checkReady().then =&gt;
      @_adapter.stream(@_name, @_conditions, @_options)
      .on 'error', (error) -&gt;
        transformer.emit 'error', error
      .pipe transformer
    transformer

  ##
  # Explains the query
  # @return {Object}
  # @promise
  # @nodejscallback
  explain: (callback) -&gt;
    @_options.cache = null
    @_options.explain = true
    @_includes = []
    @exec skip_log: true, callback

  ##
  # Executes the query as a promise (.then == .exec().then)
  # @param {Function} fulfilled
  # @param {Function} rejected
  # @promise
  then: (fulfilled, rejected) -&gt;
    @exec().then fulfilled, rejected

  ##
  # Executes the query as a count operation
  # @return {Number}
  # @promise
  # @nodejscallback
  # @see AdapterBase::count
  count: (callback) -&gt;
    @_model._checkReady().then =&gt;
      if @_id or @_find_single_id
        @_conditions.push id: @_id
        delete @_id
      @_adapter.countAsync @_name, @_conditions, @_options
    .nodeify bindDomain callback

  ##
  # @private
  _validateAndBuildSaveData: (errors, data, updates, path, object) -&gt;
    model = @_model
    schema = model._schema
    for column of object
      property = schema[path+column]
      if property
        try
          model._validateColumn updates, path+column, property, true
        catch error
          errors.push error
        model._buildSaveDataColumn data, updates, path+column, property, true
      else if not object[column] and model._intermediate_paths[column]
        # set all nested columns null
        column += '.'
        temp = {}
        Object.keys(schema).forEach (sc) -&gt;
          temp[sc.substr(column.length)] = null if sc.indexOf(column) is 0
        @_validateAndBuildSaveData errors, data, updates, path + column, temp
      else if typeof object[column] is 'object'
        @_validateAndBuildSaveData errors, data, updates, path + column + '.', object[column]

  ##
  # Executes the query as a update operation
  # @param {Object} updates
  # @return {Number}
  # @promise
  # @nodejscallback
  # @see AdapterBase::count
  update: (updates, callback) -&gt;
    @_model._checkReady().then =&gt;
      errors = []
      data = {}
      @_validateAndBuildSaveData errors, data, updates, '', updates
      if errors.length &gt; 0
        return Promise.reject new Error errors.join ','

      if @_id or @_find_single_id
        @_conditions.push id: @_id
        delete @_id
      @_connection.log @_name, 'update', data: data, conditions: @_conditions, options: @_options
      @_adapter.updatePartialAsync @_name, data, @_conditions, @_options
    .nodeify bindDomain callback

  ##
  # Executes the query as an insert or update operation
  # @param {Object} updates
  # @return {Number}
  # @promise
  # @nodejscallback
  # @see AdapterBase::count
  upsert: (updates, callback) -&gt;
    @_model._checkReady().then =&gt;
      errors = []
      data = {}
      @_validateAndBuildSaveData errors, data, updates, '', updates
      if errors.length &gt; 0
        return Promise.reject new Error errors.join ','

      if @_id or @_find_single_id
        @_conditions.push id: @_id
        delete @_id
      @_connection.log @_name, 'upsert', data: data, conditions: @_conditions, options: @_options
      @_adapter.upsertAsync @_name, data, @_conditions, @_options
    .nodeify bindDomain callback

  _doIntegrityActions: (integrities, ids) -&gt;
    promises = integrities.map (integrity) =&gt;
      if integrity.type is 'parent_nullify'
        integrity.child.update _.zipObject([integrity.column], [null]), _.zipObject([integrity.column], [ids])
      else if integrity.type is 'parent_restrict'
        integrity.child.count _.zipObject [integrity.column], [ids]
        .then (count) -&gt;
          Promise.reject new Error 'rejected' if count&gt;0
      else if integrity.type is 'parent_delete'
        integrity.child.delete _.zipObject [integrity.column], [ids]
    Promise.all promises

  ##
  # @private
  _doArchiveAndIntegrity: (options) -&gt;
    need_archive = @_model.archive
    integrities = @_model._integrities.filter (integrity) -&gt; integrity.type.substr(0, 7) is 'parent_'
    need_child_archive = integrities.some (integrity) =&gt; integrity.child.archive
    need_integrity = need_child_archive or (integrities.length &gt; 0 and not @_adapter.native_integrity)
    return Promise.resolve() if not need_archive and not need_integrity

    # find all records to be deleted
    query = @_model.where @_conditions
    query.select '' if not need_archive # we need only id field for integrity
    query.exec skip_log: options?.skip_log
    .then (records) =&gt;
      return Promise.resolve records if not need_archive
      archive_records = records.map (record) =&gt; model: @_name, data: record
      @_connection.models['_Archive'].createBulk archive_records
      .then -&gt;
        Promise.resolve records
    .then (records) =&gt;
      return Promise.resolve() if not need_integrity
      return Promise.resolve() if records.length is 0
      ids = records.map (record) -&gt; record.id
      @_doIntegrityActions integrities, ids

  ##
  # Executes the query as a delete operation
  # @param {Object} [options]
  # @param {Boolean} [options.skip_log=false]
  # @return {Number}
  # @promise
  # @nodejscallback
  # @see AdapterBase::delete
  delete: (options, callback) -&gt;
    if typeof options is 'function'
      callback = options
      options = {}
    @_model._checkReady().then =&gt;
      if @_id or @_find_single_id
        @_conditions.push id: @_id
        delete @_id
      @_connection.log @_name, 'delete', conditions: @_conditions if not options?.skip_log

      @_doArchiveAndIntegrity options
      .then =&gt;
        @_adapter.deleteAsync @_name, @_conditions
    .nodeify bindDomain callback

module.exports = Query</pre></div></div></div><script src="http://code.jquery.com/jquery-1.11.0.min.js"></script><script src="../bootstrap-3.2.0-dist/js/bootstrap.min.js"></script><script src="../google-code-prettify/prettify.js"></script><script src="../script.js"></script><script src="../group-examples.js"></script><a href="https://github.com/croquiscom/cormo"><img class="github-ribbon" src="https://s3.amazonaws.com/github/ribbons/forkme_right_green_007200.png" alt="Fork me on GitHub"></a></body></html>